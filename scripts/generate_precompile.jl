using Pkg

# add dependencies
# ----------------

package_dir = normpath(joinpath(@__DIR__, ".."))
atomjl_file = joinpath(package_dir, "src", "Atom.jl")
project_file = joinpath(package_dir, "Project.toml")
test_file = joinpath(package_dir, "test", "runtests.jl")
precompile_file = joinpath(package_dir, "src", "precompile.jl")

lines = readlines(atomjl_file; keep = true)

toml = Pkg.TOML.parsefile(project_file)
test_deps = get(toml, "extras", nothing)

@info "Adding temporary dependencies ..."
test_deps !== nothing && Pkg.add([PackageSpec(; name = name, uuid = uuid) for (name, uuid) in test_deps])
Pkg.add("SnoopCompile")
Pkg.add("SnoopCompileCore")

# generate and assert
# -------------------

Pkg.activate(normpath(@__DIR__, ".."))
Pkg.instantiate()
push!(LOAD_PATH, normpath(@__DIR__, ".."))

using SnoopCompile
using SnoopCompileCore

try
    @info "Generating `precompile` statements ..."
    try
        open(atomjl_file, "w") do io
            for line in lines
                if occursin("_precompile_()", line)
                    write(io, "# _precompile_()\n") # comment out
                elseif occursin("include(\"docs.jl\")", line)
                    write(io, "# include(\"docs.jl\")\n")
                else
                    write(io, line)
                end
            end
        end
        @debug "Commented out `_precompile_` call in $atomjl_file for `precompile` statement generation"

        inf_timing = @snoop_inference include(test_file)
        ttot, pclist = SnoopCompile.parcel(inf_timing; excluded_modules = Set([Main]))

        atom_pair = findfirst(p -> p.first === Atom, pclist)
        if atom_pair !== nothing
            _, tmi = pclist[atom_pair].second
            strs, _ = getfield(SnoopCompile, :get_reprs)(tmi; suppress_time=true)
            stmts = strs
            open(precompile_file, "w") do io
                println(io, "# This file is mostly generated by `scripts/generate_precompile.jl`\n")
                if any(str->occursin("__lookup", str), stmts)
                    println(io, SnoopCompile.lookup_kwbody_str)
                end
                println(io, "function _precompile_()")
                println(io, "    ccall(:jl_generating_output, Cint, ()) == 1 || return nothing")
                for stmt in sort(stmts)
                    parts = split(stmt, "   # time: ")
                    base_stmt = parts[1]
                    timing = length(parts) > 1 ? "   # time: " * parts[2] : ""

                    if startswith(base_stmt, "isdefined")
                        println(io, "    try; $(base_stmt); catch err; @debug err; end$(timing)")
                    else
                        println(io, "    try; @assert($(base_stmt)); catch err; @debug err; end$(timing)")
                    end
                end
                println(io, "end")
            end
        end
    catch e
        printstyled(
            "`precompile` statement generation failed with the following error:\n";
            bold = true, color = :lightred
        )
        @error e
    end

    @info "Asserting generated statements ..."
    try
        open(atomjl_file, "w") do io
            for line in lines
                if occursin("# _precompile_()", line)
                    write(io, "_precompile_()\n") # comment in
                elseif occursin("# include(\"docs.jl\")", line)
                    write(io, "include(\"docs.jl\")\n")
                else
                    write(io, line)
                end
            end
        end
        @debug "Commented in `_precompile_` call in $atomjl_file for `precompile` statement assertion"

        julia_cmd = `$(Base.julia_cmd()[1])`
        run(pipeline(`$julia_cmd --project=. --color=yes -e '
            ENV["JULIA_DEBUG"] = "Atom"
            using Pkg
            Pkg.instantiate()
            using Atom # should invoke precompile
        '`))
    catch e
        printstyled(
            "`precompile` statement assertion failed with the following error:\n";
            bold = true, color = :lightred
        )
        @error e
    finally
        # let lines = readlines(precompile_file; keep = true)
        #     open(precompile_file, "w") do io
        #         for line in lines
        #             write(io, replace(line, "@assert " => ""))
        #         end
        #     end
        # end
        # @info "Removed `@assert`s in $precompile_file"
    end
catch e
    printstyled(
        "Unexpected error happened:\n";
        bold = true, color = :lightred
    )
    @error e
finally
    open(atomjl_file, "w") do io
        for line in lines
            write(io, line)
        end
    end
    @info "Restored the original state in $atomjl_file"
end

# remove dependencies
# -------------------

@info "Removing temporary dependencies ..."
test_deps === nothing || Pkg.rm([PackageSpec(; name = name, uuid = uuid) for (name, uuid) in test_deps])
Pkg.rm("SnoopCompile")
